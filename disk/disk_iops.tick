dbrp "hostmonitor"."autogen"
var period_time = 2m
var every_time  = 5m
var warn_value  = 1500
var crit_value  = 5000

// all read, write unit is iops
var disk_data = batch
   |query('''
       SELECT derivative("reads") as rps, derivative("writes") as wps 
       FROM "hostmonitor"."autogen"."diskio"
     ''')
     .groupBy('dc', 'host', 'name')
     .period(period_time)
     .every(every_time)
     .align()

var rps_data = disk_data
  |mean('rps')
    .as('rps_rate')

var wps_data = disk_data
  |mean('wps')
    .as('wps_rate')

rps_data
   |join(wps_data)
     .as('jrps', 'jwps')
   |alert()
      .id('{{ .TaskName }}-disk iops')
      .details('''
Time: {{ .Time.Local.Format "2006-01-02T15:04:05Z07:00" }}<br>Id: {{ .ID }}<br>Duration: {{ .Duration }}<br>Message: {{ .Message }}
''')
      .message('{{ .Level }}: {{ index .Tags "dc" }}/{{ index .Tags "host" }}/{{ index .Tags "name" }}/diskio - device: {{ index .Tags "device" }}, read: {{ index .Fields "jrps.rps_rate" | printf "%0.2f" }}iops, write: {{ index .Fields "jwps.wps_rate" | printf "%0.2f" }}iops')
      .warn(lambda: "jrps.rps_rate" + "jwps.wps_rate" > warn_value) 
      .crit(lambda: "jrps.rps_rate" + "jwps.wps_rate" > crit_value) 
      .exec('/usr/local/bin/kap-exec', '--conf', '/etc/kapalarm/kap.conf')
